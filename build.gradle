plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.gorylenko.gradle-git-properties' version '1.4.21'
}

apply plugin: 'io.spring.dependency-management'

version = '0.3'
sourceCompatibility = 1.8

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

bootJar.getArchiveVersion().set('')

springBoot  {
    buildInfo()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task dockerBuild {
    def repository = 'merusso/spring-boot-demo'
    def tag = project.version + '-1'
    def imageName = "$repository:$tag"
    
    def stageDir = new File(project.buildDir, "docker")
    
    doLast {
        copy {
            from 'src/main/docker/Dockerfile'
            from "$buildDir/libs"
            into stageDir
        }
        exec {
            println "Building docker image '$imageName'"
            executable 'docker'
            args 'build', '--squash', '-t', imageName, stageDir
        }
    }
}
